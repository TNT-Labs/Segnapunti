apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
apply plugin: "com.google.gms.google-services"
apply from: file("../../node_modules/react-native-vector-icons/fonts.gradle")

def projectRoot = rootDir.getAbsoluteFile().getParentFile().getAbsolutePath()

react {
    /* Directories */
    root = file(projectRoot)
    reactNativeDir = file("$projectRoot/node_modules/react-native")
    codegenDir = file("$projectRoot/node_modules/@react-native/codegen")
    cliFile = file("$projectRoot/node_modules/react-native/cli.js")

    /* Hermes Engine - OBBLIGATORIO per React Native 0.76+ */
    hermesEnabled = true

    /* Flags per il bundler */
    bundleCommand = "bundle"

    /* Evita problemi con variant produttivi */
    debuggableVariants = ["debug"]
}

def enableProguardInReleaseBuilds = true

// Legge i valori da gradle.properties
def isNewArchEnabled = project.hasProperty("newArchEnabled") ? project.property("newArchEnabled").toString().toBoolean() : false
def isHermesEnabled = project.hasProperty("hermesEnabled") ? project.property("hermesEnabled").toString().toBoolean() : true

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.tntlabs.segnapunti"

    defaultConfig {
        applicationId "com.tntlabs.segnapunti"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        // VERSION SYNC - MUST INCREMENT for each Play Store release
        versionCode 11
        versionName "1.0.7"

        // Configurazione New Architecture e Hermes
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchEnabled.toString()
        buildConfigField "boolean", "IS_HERMES_ENABLED", isHermesEnabled.toString()

        // Configurazione NDK rimossa completamente per consentire
        // l'inclusione automatica di tutte le architetture da gradle.properties
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (project.hasProperty('SEGNAPUNTI_UPLOAD_STORE_FILE')) {
                storeFile file(SEGNAPUNTI_UPLOAD_STORE_FILE)
                storePassword SEGNAPUNTI_UPLOAD_STORE_PASSWORD
                keyAlias SEGNAPUNTI_UPLOAD_KEY_ALIAS
                keyPassword SEGNAPUNTI_UPLOAD_KEY_PASSWORD
            }
        }
    }

    // SPLITS COMPLETAMENTE DISABILITATI - APK universale unico
    // splits {
    //     abi {
    //         reset()
    //         enable false
    //     }
    // }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false  // TEMPORANEAMENTE DISABILITATO per debug librerie native
            shrinkResources false  // TEMPORANEAMENTE DISABILITATO per debug librerie native
            crunchPngs false  // Disabilita PNG crunching per evitare errori AAPT2 su Windows
            // proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    buildFeatures {
        buildConfig true  // Necessario per generare BuildConfig.java con i campi custom
    }

    packagingOptions {
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'

        // Include tutte le librerie Hermes
        pickFirst 'lib/x86/libhermes.so'
        pickFirst 'lib/x86_64/libhermes.so'
        pickFirst 'lib/armeabi-v7a/libhermes.so'
        pickFirst 'lib/arm64-v8a/libhermes.so'

        // RICHIESTO per HyperOS/MIUI: estrae le .so su disco durante l'installazione.
        // Senza questo, il kernel di MIUI blocca il caricamento diretto dall'APK
        // e SoLoader fallisce con "library not found" per libreact_featureflagsjni.so
        // e altre librerie native di React Native New Architecture.
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

dependencies {
    // React Native core
    implementation("com.facebook.react:react-android")

    // Hermes Engine - versione gestita dal BOM del react-native-gradle-plugin.
    // NON usare le varianti -debug/-release: sono AAR separati che non includono
    // libhermes_executor.so (necessaria per Old Architecture). L'AAR base include tutto.
    implementation("com.facebook.react:hermes-android")

    // Moduli React Native - NECESSARI per il classpath Java
    implementation project(':react-native-async-storage_async-storage')
    implementation project(':react-native-gesture-handler')
    implementation project(':react-native-google-mobile-ads')
    implementation project(':react-native-safe-area-context')
    implementation project(':react-native-screens')
    implementation project(':react-native-vector-icons')
}
